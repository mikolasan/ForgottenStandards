version = '0.0.1'
description = 'ratio-generator'

apply plugin: 'java-library'
apply plugin: 'kotlin'


dependencies {
    //noinspection DifferentStdlibGradleVersion
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.squareup:kotlinpoet:1.5.0'
}

sourceCompatibility = '1.8'

task runRatioGenerator(type: JavaExec) {
    group = 'units'
    classpath = sourceSets.main.runtimeClasspath
    main = 'io.github.mikolasan.ratiogenerator.RatioGeneratorKt'
    // store the output instead of printing to the console:
    standardOutput = new ByteArrayOutputStream()
    // extension method genSource.output() can be used to obtain the output:
    doLast {
        ext.generated = standardOutput.toString()
    }
}

task saveGeneratedSources(dependsOn: runRatioGenerator) {
    group = 'units'

    def outputDir = new File(sourceSets.main.java.srcDirs.first(), "io/github/mikolasan/ratiogenerator")
    def outputFile = new File(outputDir, "WeightUnits.kt")
//    def outputDir = new File("/${buildDir}/output")
//    def outputFile = new File(outputDir, "full-routes.txt")
    doLast {
        if(!outputDir.exists()) {
            outputDir.mkdirs()
        }
        outputFile.text = tasks.runRatioGenerator.generated
    }
}

// References:
// https://stackoverflow.com/questions/38275583/create-version-txt-file-in-project-dir-via-build-gradle-task
// https://docs.gradle.org/current/dsl/org.gradle.api.tasks.Exec.html
// https://discuss.gradle.org/t/gradle-exec-first-task-output-to-second-task-commandline/19719/12
